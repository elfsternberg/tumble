// -*- mode: javascript -*-
{
    var _VALID_BLOCK_TYPES = ['if', 'when', 'template', 'many', 'block'];
    var _VBT_LENGTH = _VALID_BLOCK_TYPES.length;

    function is_valid_block_type(b) {
        for(i = 0; i < _VBT_LENGTH; i++) {
            if (_VALID_BLOCK_TYPES[i] == b) {
                return true;
            }
        }
        return false;
    }
}


document
    = ps:part* 
      { return { unit: "block", name: "document", content: ps }; }

part
    = block / variable / text

tag_start "tag_start"
    =  ld b:tagname ":" n:tagname rd
    &{ return is_valid_block_type(b); }
    { return {type: b, name: n }; }

tag_end
    =  ld '/' b:tagname ":" n:tagname rd 
    { return {type: b, name: n }; }

tagname "tagname"
    = t:[a-zA-Z]+
    { return t.join(''); }

tag
    = ld (!rd !eol [a-zA-Z\:\/])+ rd

ld
    = "{"

rd
    = "}"

eol
    = "\n"
    / "\r\n"
    / "\r"
    / "\u2028"
    / "\u2029"

text
    = bs:(!tag c:. {return c})+ 
      { return { unit: 'text', content: bs.join('') } }

variable "variable"
    = ld t:tagname rd 
      { return { unit: 'variable', name: t }; }

block
    = t:tag_start ps:part* n:tag_end
      &{ return (t.type == n.type) && (t.name == n.name) } 
      { return {unit: 'block', type:t.type, name:t.name, content: ps } }

